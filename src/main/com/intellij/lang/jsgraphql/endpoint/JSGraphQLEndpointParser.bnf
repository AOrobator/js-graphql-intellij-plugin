/**
 * Copyright (c) 2015-present, Jim Kynde Meyer
 * All rights reserved.
 * <p>
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */
{

    parserClass="com.intellij.lang.jsgraphql.endpoint.parser.JSGraphQLEndpointParser"

    psiClassPrefix="JSGraphQLEndpoint"
    psiImplClassSuffix="Impl"
    psiPackage="com.intellij.lang.jsgraphql.endpoint.psi"
    psiImplPackage="com.intellij.lang.jsgraphql.endpoint.psi.impl"

    extends="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointPsiElement"

    elementTypeHolderClass="com.intellij.lang.jsgraphql.endpoint.JSGraphQLEndpointTokenTypes"
    elementTypePrefix=""
    elementTypeClass="com.intellij.lang.jsgraphql.endpoint.JSGraphQLEndpointTokenType"
    tokenTypeClass="com.intellij.lang.jsgraphql.endpoint.JSGraphQLEndpointTokenType"

    tokens=[

        TYPE='type'
        INTERFACE='interface'
        IMPLEMENTS='implements'
        INPUT='input'
        ENUM='enum'
        UNION='union'
        SCHEMA='schema'
        QUERY='query'
        MUTATION='mutation'
        SUBSCRIPTION='subscription'
        IMPORT='import'
        LPAREN='('
        RPAREN=')'
        LBRACE='{'
        RBRACE='}'
        LBRACKET='['
        RBRACKET=']'
        COMMA=','
        COLON=':'
        EQUALS='='
        PIPE='|'
        TRUE='true'
        FALSE='false'
        REQUIRED='!'
        AT_ANNOTATION='regexp:@([_A-Za-z][_0-9A-Za-z]*)?'
        LINE_COMMENT="regexp:#.*"

        identifier='regexp:[_A-Za-z][_0-9A-Za-z]*'
        number='regexp:-?[0-9]+(\.[0-9+])?'
    ]
}

Document ::= Definition*

private Definition ::=
    ObjectTypeDefinition
    |
    InterfaceTypeDefinition
    |
    InputObjectTypeDefinition
    |
    EnumTypeDefinition
    |
    UnionTypeDefinition
    |
    ScalarTypeDefinition
    |
    ImportDeclaration
    |
    SchemaDefinition
    {recoverWhile=RecoverDefinition}

private RecoverDefinition ::= !(TYPE | INTERFACE | INPUT | ENUM | UNION | SCALAR | IMPORT | SCHEMA)

ObjectTypeDefinition ::= TYPE NamedTypeDef ImplementsInterfaces? FieldDefinitionSet {pin=1 implements="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointNamedTypeDefinition"}

ImplementsInterfaces ::= IMPLEMENTS NamedType CommaNamedType*  {pin=1}

private CommaNamedType ::= COMMA NamedType {pin=1 recoverWhile=RecoverCommaNamedType}

private RecoverCommaNamedType ::= !(LBRACE | COMMA | TYPE | INTERFACE | INPUT | ENUM | UNION | SCALAR | IMPORT | SCHEMA | identifier)

FieldDefinitionSet ::= LBRACE FieldDefinition* RBRACE {pin=1}

FieldDefinition ::= Annotations? Property ArgumentsDefinition? COLON CompositeType {pin=2 recoverWhile=RecoverFieldDefinition}

private RecoverFieldDefinition ::= !(RBRACE | AT_ANNOTATION | identifier)

ArgumentsDefinition ::= LPAREN InputValueDefinitions RPAREN {pin=1 recoverWhile=RecoverArgumentsDefinition}

private RecoverArgumentsDefinition ::= !(COLON | RBRACE | AT_ANNOTATION | identifier)

InputValueDefinitions ::= InputValueDefinition (COMMA InputValueDefinition)*

InputValueDefinition ::= identifier COLON CompositeType {pin=1}

Annotations ::= Annotation+

Annotation ::= AT_ANNOTATION AnnotationArguments? {pin=1 recoverWhile=RecoverAnnotation}

private RecoverAnnotation ::= !(RBRACE | AT_ANNOTATION | identifier)

AnnotationArgumentValue ::= TRUE | FALSE | number | QuotedString

AnnotationArguments ::= LPAREN (AnnotationArgumentValue | NamedAnnotationArguments) RPAREN {pin=1}

NamedAnnotationArguments ::= NamedAnnotationArgument CommaNamedAnnotationArgument*

private CommaNamedAnnotationArgument ::= COMMA NamedAnnotationArgument {pin=1 recoverWhile=RecoverNamedAnnotationArgument}

NamedAnnotationArgument ::= identifier EQUALS AnnotationArgumentValue {pin=1 recoverWhile=RecoverNamedAnnotationArgument}

private RecoverNamedAnnotationArgument ::= !(COMMA | RPAREN)

NamedType ::= identifier {mixin="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointNamedTypePsiElement"}

NamedTypeDef ::= identifier {mixin="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointNamedTypeDefPsiElement"}

CompositeType ::= ListType | (NamedType REQUIRED?)

ListType ::= LBRACKET NamedType RBRACKET {pin=1}

Property ::= identifier

InterfaceTypeDefinition ::= INTERFACE NamedTypeDef FieldDefinitionSet {pin=1 implements="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointNamedTypeDefinition"}

InputObjectTypeDefinition ::= INPUT NamedTypeDef FieldDefinitionSet {pin=1 implements="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointNamedTypeDefinition"}

EnumTypeDefinition ::= ENUM NamedTypeDef EnumValueDefinitionSet {pin=1 implements="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointNamedTypeDefinition"}

EnumValueDefinitionSet ::= LBRACE EnumValueDefinition CommaEnumValueDefinition* RBRACE {pin=1}

private CommaEnumValueDefinition ::= COMMA EnumValueDefinition {pin=1 recoverWhile=RecoverCommaEnumValueDefinition}

private RecoverCommaEnumValueDefinition ::= !(RBRACE | COMMA | TYPE | INTERFACE | INPUT | ENUM | UNION | SCALAR | IMPORT | SCHEMA | identifier)

EnumValueDefinition ::= identifier

UnionTypeDefinition ::= UNION NamedTypeDef EQUALS UnionMemberSet {pin=1 implements="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointNamedTypeDefinition"}

UnionMemberSet ::= UnionMember PipeUnionMember*

private PipeUnionMember ::= PIPE UnionMember {pin=1}

UnionMember ::= identifier

ScalarTypeDefinition ::= SCALAR NamedTypeDef {pin=1 implements="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointNamedTypeDefinition"}

ImportDeclaration ::= IMPORT ImportFileReference {pin=1}

ImportFileReference ::= QuotedString {implements="com.intellij.psi.PsiNameIdentifierOwner" mixin="com.intellij.lang.jsgraphql.endpoint.psi.JSGraphQLEndpointImportFileReferencePsiElement"}

SchemaDefinition ::= SCHEMA OperationTypeDefinitionSet {pin=1}

OperationTypeDefinitionSet ::= LBRACE OperationTypeDefinition* RBRACE {pin=1}

OperationTypeDefinition ::= (QUERY | MUTATION | SUBSCRIPTION) COLON NamedType {pin=1 recoverWhile=RecoverOperation}

private RecoverOperation ::= !(RBRACE | QUERY | MUTATION | SUBSCRIPTION)

QuotedString ::= OPEN_QUOTE STRING_BODY? CLOSING_QUOTE {pin=1}