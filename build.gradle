buildscript {
    repositories {
        maven { url 'https://jitpack.io'}
    }
    dependencies{
        classpath "com.github.JetBrains:gradle-grammar-kit-plugin:2018.3"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.4.3'
    id "de.undercouch.download" version "3.4.3"
}

group 'com.intellij.lang.jsgraphql'
version '2.0.0-alpha-6'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile ("com.graphql-java:graphql-java:11.0") {
        exclude group: "org.reactivestreams", module: "reactive-streams"
        exclude group: "com.graphql-java", module: "java-dataloader"
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    compile "commons-io:commons-io:2.6"
    compile "com.atlassian.commonmark:commonmark:0.12.1"
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.21'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

intellij {
    version '2018.1.5'
    type 'IU'
    updateSinceUntilBuild = false
    pluginName 'JS GraphQL'
    plugins = ['JavaScriptLanguage', 'CSS']
    ideaDependencyCachePath = project.buildDir
}

runIde {
    jvmArgs '-Xmx2G'
}

patchPluginXml {

}

sourceSets {

    main.java.srcDirs = ['src/main', 'gen']
    main.resources.srcDir 'resources'

    test.java.srcDirs = ['src/test']
    test.resources.srcDir 'test-resources'

}

apply plugin: 'org.jetbrains.grammarkit'

// import is optional to make task creation easier
import org.jetbrains.grammarkit.tasks.*

// GraphQL Parser and Lexer
task generateGraphQLLexer(type: GenerateLexer) {
    source 'src/grammars/GraphQLLexer.flex'
    targetDir 'gen/com/intellij/lang/jsgraphql/'
    targetClass 'GraphQLLexer'
    purgeOldFiles = true
}

task generateGraphQLParser(type: GenerateParser) {
    source "src/grammars/GraphQLParser.bnf"
    targetRoot = 'gen/'
    purgeOldFiles = true
    pathToParser = 'com/intellij/lang/jsgraphql/GraphQLParser'
    pathToPsiRoot = 'com/intellij/lang/jsgraphql/psi'
}

// GraphQL Endpoint Parser and Lexer
task generateGraphQLEndpointLexer(type: GenerateLexer) {
    source 'src/grammars/JSGraphQLEndpointLexer.flex'
    targetDir 'gen/com/intellij/lang/jsgraphql/endpoint/lexer/'
    targetClass 'JSGraphQLEndpointLexer'
    purgeOldFiles = true
}

task generateGraphQLEndpointParser(type: GenerateParser) {
    source "src/grammars/JSGraphQLEndpointParser.bnf"
    targetRoot = 'gen/'
    purgeOldFiles = true
    pathToParser = 'com/intellij/lang/jsgraphql/endpoint/parser/JSGraphQLEndpointParser'
    pathToPsiRoot = 'com/intellij/lang/jsgraphql/endpoint/psi'
}


// GraphQL Endpoint Doc Parser and Lexer
task generateGraphQLEndpointDocLexer(type: GenerateLexer) {
    source 'src/grammars/JSGraphQLEndpointDocLexer.flex'
    targetDir 'gen/com/intellij/lang/jsgraphql/endpoint/doc/lexer/'
    targetClass 'GraphQLLexer'
    purgeOldFiles = true
}

task generateGraphQLEndpointDocParser(type: GenerateParser) {
    source "src/grammars/JSGraphQLEndpointDocParser.bnf"
    targetRoot = 'gen/'
    purgeOldFiles = true
    pathToParser = 'com/intellij/lang/jsgraphql/endpoint/doc/JSGraphQLEndpointDocParser'
    pathToPsiRoot = 'com/intellij/lang/jsgraphql/endpoint/doc/psi'
}

task downloadMinimatchNashorn(type: Download) {
    src 'https://raw.githubusercontent.com/jimkyndemeyer/minimatch-nashorn/1.0.1/dist/minimatch-nashorn.js'
    dest 'resources/META-INF'
    overwrite false
}

compileJava {
    dependsOn downloadMinimatchNashorn
    dependsOn generateGraphQLEndpointLexer
    dependsOn generateGraphQLEndpointParser
    dependsOn generateGraphQLEndpointDocLexer
    dependsOn generateGraphQLEndpointDocParser
    dependsOn generateGraphQLLexer
    dependsOn generateGraphQLParser
}